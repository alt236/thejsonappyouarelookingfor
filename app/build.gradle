apply plugin: 'com.android.application'

final int versionMajor = 1
final int versionMinor = 0
final int versionPatch = 0

final int apkVersionCode = 1
final String apkVersionName = "${versionMajor}.${versionMinor}.${versionPatch}"

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    packagingOptions.excludes = getDefaultExcludes();

    defaultConfig {
        applicationId "uk.co.alt236.thejsonappyouaskedfor"
        minSdkVersion rootProject.minSdkVersion;
        targetSdkVersion rootProject.targetSdkVersion;

        versionCode apkVersionCode
        versionName apkVersionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "API_ENDPOINT_STRING", quoteString("http://jsonplaceholder.typicode.com/")
        buildConfigField "String", "GIT_SHA", quoteString(gitSha())
        buildConfigField "String", "BUILD_TIMESTAMP", quoteString(getNow())
        buildConfigField "String", "GIT_TIMESTAMP", quoteString(gitTimestamp())
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    productFlavors {
        prod {
            resValue("string", "app_name", "Demo App")
        }
        dev {
            applicationIdSuffix(".dev")
            resValue("string", "app_name", "Demo App Dev")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable false

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "Boolean", "DEV_MODE", "false"
        }

        debug {
            testCoverageEnabled true
            debuggable true
            buildConfigField "Boolean", "DEV_MODE", "true"
        }

//        applicationVariants.all { final variant ->
//            variant.outputs.each { final output ->
//                annotateApkFilename(variant, output);
//            }
//        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(path: ':loader')

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile "com.android.support:appcompat-v7:$rootProject.appCompatVersion"
    compile "com.android.support:design:$rootProject.appCompatVersion"
    compile "com.android.support:cardview-v7:$rootProject.appCompatVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.appCompatVersion"

    compile "com.google.dagger:dagger:$rootProject.daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"

    compile "com.squareup.picasso:picasso:2.5.2"
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'

    compile "com.jakewharton:butterknife:7.0.1"
    compile "me.grantland:autofittextview:0.2.1"

    testCompile "org.mockito:mockito-core:$rootProject.mockitoVersion"
    testCompile "junit:junit:$rootProject.junitVersion"
}
